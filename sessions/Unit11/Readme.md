# Terraform


### Basic resource definitions

User for provisioning resources

```
resource "aws_instance" "helloworld" {
  ami           = "ami-09dd2e08d601bff67"
  instance_type = "t2.micro"
  tags = {
    Name = "HelloWorld"
  }
}
```


```
resource "local_file" "literature" {
    filename = "art_of_war.txt"
    content     = <<-EOT
      Sun Tzu said: The art of war is of vital importance to the State.

      It is a matter of life and death, a road either to safety or to 
      ruin. Hence it is a subject of inquiry which can on no account be
      neglected.
    EOT
}
```

### Data resources

Used for retrieving information from underlying infrastructure
```
data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }

  owners = ["099720109477"]
}

```

```
data "aws_eks_cluster" "default" {
  name = module.eks.cluster_id
}

data "aws_eks_cluster_auth" "default" {
  name = module.eks.cluster_id
}
```


### variables
Used to make code portable and configurable
```
variable "words" {
  description = "A word pool to use for Mad Libs"
  type = object({
    nouns      = list(string),
    adjectives = list(string),
    verbs      = list(string),
    adverbs    = list(string),
    numbers    = list(number),
  })
}

variable "maintenance_exclusions" {
  type        = list(object({ name = string, start_time = string, end_time = string, exclusion_scope = string }))
  description = "List of maintenance exclusions. A cluster can have up to three"
  default     = []
}

variable "maintenance_end_time" {
  type        = string
  description = "Time window specified for recurring maintenance operations in RFC3339 format"
  default     = ""
}

variable "windows_node_pools" {
  type        = list(map(string))
  description = "List of maps containing Windows node pools"
  default     = []
}

variable "node_pools_labels" {
  type        = map(map(string))
  description = "Map of maps containing node labels by node-pool name"

  # Default is being set in variables_defaults.tf
  default = {
    all               = {}
    default-node-pool = {}
  }
}


variable "cluster_autoscaling" {
  type = object({
    enabled       = bool
    min_cpu_cores = number
    max_cpu_cores = number
    min_memory_gb = number
    max_memory_gb = number
    gpu_resources = list(object({ resource_type = string, minimum = number, maximum = number }))
    auto_repair   = bool
    auto_upgrade  = bool
    disk_size     = optional(number)
    disk_type     = optional(string)
  })
  default = {
    enabled       = false
    max_cpu_cores = 0
    min_cpu_cores = 0
    max_memory_gb = 0
    min_memory_gb = 0
    gpu_resources = []
    auto_repair   = true
    auto_upgrade  = true
    disk_size     = 100
    disk_type     = "pd-standard"
  }
  description = "Cluster autoscaling configuration. See [more details](https://cloud.google.com/kubernetes-engine/docs/reference/rest/v1beta1/projects.locations.clusters#clusterautoscaling)"
}
```

### outputs
Used to expose information from Terraform provisioned infrastructure
Example output definitions

```
output "cluster_id" {
  description = "EKS cluster ID."
  value       = module.eks.cluster_id
}

output "cluster_endpoint" {
  description = "Endpoint for EKS control plane."
  value       = module.eks.cluster_endpoint
}

output "cluster_security_group_id" {
  description = "Security group ids attached to the cluster control plane."
  value       = module.eks.cluster_security_group_id
}

output "kubectl_config" {
  description = "kubectl config as generated by the module."
  value       = module.eks.kubeconfig
}

output "config_map_aws_auth" {
  description = "A kubernetes configuration to authenticate to this EKS cluster."
  value       = module.eks.config_map_aws_auth
}

output "region" {
  description = "AWS region"
  value       = var.region
}

output "cluster_name" {
  description = "Kubernetes Cluster Name"
  value       = local.cluster_name
}
```


### locals
Used for setting values
```
locals {
  cluster_name = "education-eks"
  cluster_master_auth_list_layer1 = local.cluster_output_master_auth
  cluster_master_auth_list_layer2 = local.cluster_master_auth_list_layer1[0]
  cluster_master_auth_map         = local.cluster_master_auth_list_layer2[0]
}

```

### functions

```
concat
merge
length
zipmap
compact
toset
join
slice
split
sort

```


### conditionals
```
cluster_maintenance_window_is_recurring = var.maintenance_recurrence != "" && var.maintenance_end_time != "" ? [1] : []
cluster_maintenance_window_is_daily     = length(local.cluster_maintenance_window_is_recurring) > 0 ? [] : [1]

```

### modules

Modules are unit of Terraform code/logic that deliver a specific capability or infrastructure
All working Terraform code are modules provided they are well-define and have a well defined interface (variable set)
You can call local modules as well as external modules
When calling a child module ensure all its mandatory variables are speficied, otherwise the call will fail
From your parent module, you can output values that are also outputted in the child module.

```
module "autoscaling" {
  source      = "./modules/autoscaling"    ## calling local child module
  namespace   = var.namespace  
  ssh_keypair = var.ssh_keypair  
  vpc       = module.networking.vpc  
  sg        = module.networking.sg  
  db_config = module.database.db_config  
}

output "autoscaling_name" {
  description = "autoscaling"
  value       = module.autoscaling.name
}


module "database" {
  source    = "./modules/database" ## calling local child module
  namespace = var.namespace  
  vpc = module.networking.vpc  
  sg  = module.networking.sg  

}

module "networking" {
  source    = "./modules/networking"  ## calling local child module
  namespace = var.namespace  
}

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws" ## calling external child module
  version = "3.2.0"    ## pinning to a specific version

  name                 = "education-vpc"
  cidr                 = "10.0.0.0/16"
  azs                  = data.aws_availability_zones.available.names
  private_subnets      = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnets       = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  enable_nat_gateway   = true
  single_nat_gateway   = true
  enable_dns_hostnames = true

  tags = {
    "kubernetes.io/cluster/${local.cluster_name}" = "shared"
  }

  public_subnet_tags = {
    "kubernetes.io/cluster/${local.cluster_name}" = "shared"
    "kubernetes.io/role/elb"                      = "1"
  }

  private_subnet_tags = {
    "kubernetes.io/cluster/${local.cluster_name}" = "shared"
    "kubernetes.io/role/internal-elb"             = "1"
  }
}

output "vpc_id" {
  description = "vpc ID"
  value       = module.vpc.vpc_id
}

```

### Backend configuration
```
terraform {
  backend "s3" {
    bucket         = "techoutcomes-?????-terraform-backend"
    encrypt        = true
    key            = "eks/simpleeks-terraform.tfstate"
    region         = "eu-west-1"
    #dynamodb_table = "techoutcomes-3tier-terraform-backend"
  }
}

```

### Reference
1) https://github.com/terraform-in-action/manning-code.git
2) 

